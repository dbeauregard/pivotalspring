plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.2'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.gorylenko.gradle-git-properties' version '2.4.2'
	id 'com.github.spotbugs' version "6.0.25"
	id 'checkstyle'
	id 'jacoco'
	id 'maven-publish'
	id 'org.cyclonedx.bom' version '1.8.2'
}

group = 'io.dbeauregard'
version = project.properties['version-base'] + "-" + project.properties['version-build'] + project.properties['version-archiveClassifier']

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

jar {
    enabled = false //disable plain jar
}

repositories {
  mavenCentral()
  maven { url 'https://repo.spring.io/milestone' }
  maven { url 'https://repo.spring.io/snapshot' }
}

ext {
  set('springAiVersion', "1.0.0-M6")
  set('openapiVersion', '2.7.0')
}

dependencies {
	implementation platform("org.springframework.ai:spring-ai-bom:${springAiVersion}")
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${openapiVersion}"
	implementation 'io.swagger.core.v3:swagger-annotations:2.2.26'  //TODO: Temporary fix for Swagger
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-hateoas'
	implementation 'org.springframework.ai:spring-ai-ollama-spring-boot-starter'
	implementation 'org.springframework.ai:spring-ai-pgvector-store-spring-boot-starter'
	// implementation 'org.springframework.ai:spring-ai-neo4j-store-spring-boot-starter'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	developmentOnly 'org.springframework.boot:spring-boot-docker-compose'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'org.springframework.boot:spring-boot-testcontainers'
  	testImplementation 'org.testcontainers:junit-jupiter'
  	testImplementation 'org.testcontainers:postgresql'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'org.postgresql:postgresql'
}

tasks.named('test') {
	useJUnitPlatform()
}

test {
	finalizedBy jacocoTestReport
}

checkstyle {
  ignoreFailures = true
  showViolations = false
//   maxWarnings = 0
}

spotbugs {
//   toolVersion = '4.8.6'
  ignoreFailures = true
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
  reports {
    xml {
      enabled = false
    }
    html {
      enabled = true
    }
  }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/dbeauregard/pivotalspring")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            // from(components.java)
			artifact bootJar
        }
    }
}